programa parcial3
procesos

  proceso procesarEsquina (E x: numero ; E y: numero ; ES cantFlores: numero ; E avenidaInicial: numero ; E calleInicial: numero)
  comenzar
    BloquearEsquina (x, y)
    Pos (x, y)
    juntarFlores (cantFlores)
    Pos (avenidaInicial, calleInicial)
    LiberarEsquina (x, y)
  fin


  proceso juntarFlores (ES cantFlores: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cantFlores := cantFlores + 1
  fin

  
  proceso depositarFlores (E cantFlores: numero ; E avenidaInicial: numero ; E calleInicial: numero)
  comenzar
    BloquearEsquina (10, 10)
    Pos (10, 10)
    repetir cantFlores
      depositarFlor
    Pos (avenidaInicial, calleInicial)
    LiberarEsquina (10, 10)
  fin


  proceso finalizarFloreros
  comenzar
    EnviarMensaje (F, robotFlorero1)
    EnviarMensaje (F, robotFlorero2)
    EnviarMensaje (F, robotFlorero3)
  fin


areas
  areaCuadrante: AreaPC (40, 40, 60, 60)
  esquinaDeposito: AreaPC (10, 10, 10, 10)
  esquinaFiscalizador: AreaP (4, 1, 4, 1)
  esquinaFlorero1: AreaP (1, 1, 1, 1)
  esquinaFlorero2: AreaP (2, 1, 2, 1)
  esquinaFlorero3: AreaP (3, 1, 3, 1)
robots

  robot tipoFiscalizador
  variables
    ID, x, y, cantAux, cantTotal: numero
  comenzar
    cantTotal := 0
    repetir 8
      Random (ID, 1, 3)
      Random (x, 40, 60)
      Random (y, 40, 60)
      si (ID = 1)
        EnviarMensaje (V, robotFlorero1)
        EnviarMensaje (x, robotFlorero1)
        EnviarMensaje (y, robotFlorero1)
      sino
        si (ID = 2)
          EnviarMensaje (V, robotFlorero2)
          EnviarMensaje (x, robotFlorero2)
          EnviarMensaje (y, robotFlorero2)
        sino
          EnviarMensaje (V, robotFlorero3)
          EnviarMensaje (x, robotFlorero3)
          EnviarMensaje (y, robotFlorero3)
    finalizarFloreros
    repetir 3
      RecibirMensaje (cantAux, *)
      cantTotal := cantTotal + cantAux
    Informar (cantTotal)
  fin
  

  robot tipoFlorero
  variables
    ok: boolean
    ID, x, y, avenidaInicial, calleInicial, cantFlores: numero
  comenzar
    cantFlores := 0
    avenidaInicial := PosAv
    calleInicial := PosCa
    ok := V
    RecibirMensaje (ok, robotFiscalizador)
    mientras (ok)
      RecibirMensaje (x, robotFiscalizador)
      RecibirMensaje (y, robotFiscalizador)
      procesarEsquina (x, y, cantFlores, avenidaInicial, calleInicial)
      RecibirMensaje (ok, robotFiscalizador)
    depositarFlores (cantFlores, avenidaInicial, calleInicial)
    EnviarMensaje (cantFlores, robotFiscalizador)
  fin


variables
  robotFiscalizador: tipoFiscalizador
  robotFlorero1: tipoFlorero
  robotFlorero2: tipoFlorero
  robotFlorero3: tipoFlorero
comenzar
  AsignarArea (robotFiscalizador, esquinaFiscalizador)
  AsignarArea (robotFlorero1, esquinaFlorero1)
  AsignarArea (robotFlorero2, esquinaFlorero2)
  AsignarArea (robotFlorero3, esquinaFlorero3)
  AsignarArea (robotFlorero1, esquinaDeposito)
  AsignarArea (robotFlorero2, esquinaDeposito)
  AsignarArea (robotFlorero3, esquinaDeposito)
  AsignarArea (robotFlorero1, areaCuadrante)
  AsignarArea (robotFlorero2, areaCuadrante)
  AsignarArea (robotFlorero3, areaCuadrante)
  Iniciar (robotFiscalizador, 4, 1)
  Iniciar (robotFlorero1, 1, 1)
  Iniciar (robotFlorero2, 2, 1)
  Iniciar (robotFlorero3, 3, 1)
fin
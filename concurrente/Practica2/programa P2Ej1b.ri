programa P2Ej1b
procesos
  proceso juntarFlores (ES cantF: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      cantF := cantF + 1
  fin
  proceso recorrerAvenida (ES cantF: numero)
  comenzar
    repetir 9
      juntarFlores (cantF)
      mover
    juntarFlores (cantF)
  fin
areas
  ciudad1: AreaP (1, 1, 1, 10)
  ciudad2: AreaP (2, 11, 2, 20)
  ciudad3: AreaP (2, 1, 2, 1)
robots
  robot tipoJefe
  variables
    quienEs, cantF, cantR1, cantR2, diferencia: numero
  comenzar
    EnviarMensaje (1, robot1)
    EnviarMensaje (2, robot2)
    repetir 2
      RecibirMensaje (quienEs, *)
      si (quienEs = 1)
        RecibirMensaje (cantF, robot1)
        cantR1 := cantF
      sino
        RecibirMensaje (cantF, robot2)
        cantR2 := cantF
    si (cantR1 > cantR2)
      diferencia := cantR1 - cantR2
    sino
      diferencia := cantR2 - cantR1
    Informar (diferencia)
  fin
  robot tipoJuntador
  variables
    quienSoy, cantF: numero
  comenzar
    RecibirMensaje (quienSoy, robot3)
    cantF := 0
    recorrerAvenida (cantF)
    EnviarMensaje (quienSoy, robot3)
    EnviarMensaje (cantF, robot3)
  fin
variables
  robot1: tipoJuntador
  robot2: tipoJuntador
  robot3: tipoJefe
comenzar
  AsignarArea (robot1, ciudad1)
  AsignarArea (robot2, ciudad2)
  AsignarArea (robot3, ciudad3)
  Iniciar (robot1, 1, 1)
  Iniciar (robot2, 2, 11)
  Iniciar (robot3, 2, 1)
fin